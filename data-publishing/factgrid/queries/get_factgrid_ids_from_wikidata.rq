#defaultView:Table

  # Prefixes
  PREFIX fg: <https://database.factgrid.de/entity/>
  PREFIX fgt: <https://database.factgrid.de/prop/direct/>
  PREFIX wdt: <http://www.wikidata.org/prop/direct/>
  PREFIX wd: <http://www.wikidata.org/entity/>
  PREFIX wikibase: <http://wikiba.se/ontology#>
  PREFIX bd: <http://www.bigdata.com/rdf#>
  PREFIX schema: <http://schema.org/>

  SELECT DISTINCT ?fg_item ?fg_itemLabel ?fg_item_as_string ?wd_item where {

    # labels from Factgrid
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". } 
    ?fg_item fgt:P131 fg:Q400012.
    # get those Factgrid IDs that don't have a Wikidata QID
    FILTER NOT EXISTS {
      ?link schema:about ?fg_item .
      ?link schema:isPartOf <https://www.wikidata.org/> . #Targeting Wikipedia language where subjects has no article.
    }
    # Convert Factgrid ID from IRI to string
    BIND(REPLACE(STR(?fg_item), "https://database.factgrid.de/entity/", "") as ?fg_item_as_string) 
    # get those Items from Wikidata that have that corresponding Factgrid ID
    SERVICE <https://query.wikidata.org/sparql> {
      ?wd_item wdt:P8168 ?fg_item_as_string
    }
  }