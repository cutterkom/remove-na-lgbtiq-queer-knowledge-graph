# Prefix standard 
# Factgrid
PREFIX fg: <https://database.factgrid.de/entity/>
PREFIX fgt: <https://database.factgrid.de/prop/direct/>
PREFIX fgp: <https://database.factgrid.de/prop/>
PREFIX fgps: <https://database.factgrid.de/prop/statement/>
# DBpedia
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX dbr: <http://dbpedia.org/resource/> 
# Wikidata
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
# misc
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <http://schema.org/>
prefix foaf:  <http://xmlns.com/foaf/0.1/> 

#defaultView:Graph
SELECT ?fg_item ?fg_itemLabel ?property ?propertyLabel ?fg_value ?fg_valueLabel ?wd_item ?wd_value WHERE {
    # Starting Point:
    BIND(fg:Q225307 AS ?fg_item)
    #VALUES ?fg_item { fg:Q404255 fg:Q404280 fg:Q405756}
    #?fg_item fgt:P131 fg:Q400012.

    # active entites: Humans and Organisations
    VALUES ?fg_entities { fg:Q7 fg:Q12 fg:Q11214}

    # Get first connections/statements
    ?fg_item ?fgt ?fg_value.
    ?property wikibase:directClaim ?fgt.
    ?fg_value fgp:P2 ?statement1.
    ?statement1 (fgps:P2/(fgt:P3*)) ?fg_entities.

    # transform wikidata qid in factgrid to wikidata entity iri
    ?link schema:about ?fg_item .
    ?link schema:isPartOf <https://www.wikidata.org/> . 
    ?link schema:name ?qid.
    BIND(IRI(CONCAT(STR(wd:), ?qid)) AS ?wd_item)


    # transform wikidata qid in factgrid to wikidata entity iri
    ?link_value schema:about ?fg_value .
    ?link_value schema:isPartOf <https://www.wikidata.org/> . 
    ?link_value schema:name ?qid_value.
    BIND(IRI(CONCAT(STR(wd:), ?qid_value)) AS ?wd_value)

    # Wikidata
    SERVICE <https://query.wikidata.org/sparql> { 
        ?wd_item ?wdt1 ?wd_value.
      
    }  

    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }
# LIMIT 100