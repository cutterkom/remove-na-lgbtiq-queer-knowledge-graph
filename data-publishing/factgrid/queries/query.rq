# Prefix standard 
# Factgrid
PREFIX fg: <https://database.factgrid.de/entity/>
PREFIX fgt: <https://database.factgrid.de/prop/direct/>
# DBpedia
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX dbr: <http://dbpedia.org/resource/> 
# Wikidata
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
# misc
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>


SELECT distinct ?wd_item ?db_wikilink ?db_wikilink_wd_item ?image where {
    SERVICE <https://dbpedia.org/sparql> {
      # get Wikidata QID from DBPedia resource
      dbr:Magnus_Hirschfeld owl:sameAs ?wd_item FILTER(regex(str(?wd_item), "www.wikidata.org" )).
      
      # Links from Hirschfeld's Wikipedia Page to other Wikipedia pages of humans
      # also get the Wikidata QIDs of those linked humans
      OPTIONAL {dbr:Magnus_Hirschfeld dbo:wikiPageWikiLink ?db_wikilink .
               ?db_wikilink a dbo:Person .
               ?db_wikilink owl:sameAs ?db_wikilink_wd_item FILTER(regex(str(?db_wikilink_wd_item), "www.wikidata.org" ))}
    }  
   SERVICE <https://query.wikidata.org/sparql> {
	  
   OPTIONAL {
     # get images of those linked people, from wikidata
     ?db_wikilink_wd_item wdt:P18 ?image }
   }	
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }  	
}