# Prefix standard
# Factgrid
PREFIX fg: <https://database.factgrid.de/entity/>
PREFIX fgt: <https://database.factgrid.de/prop/direct/>
PREFIX fgp: <https://database.factgrid.de/prop/>
PREFIX fgpsv: <https://database.factgrid.de/prop/statement/value/>

# Wikidata
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdp: <http://www.wikidata.org/prop/>
PREFIX wdpsv: <http://www.wikidata.org/prop/statement/value/>

# misc
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT 
?fg_item 
?fg_itemLabel 
?wd_item
?fg_value
?wd_value
?is_same

where {
BIND(fg:Q376282 as ?fg_item)
#", input_items_filter, "
?fg_item fgt:P131 fg:Q400012.


# which property in focus?
BIND(fgt:P38 as ?fg_property)
BIND(fg:P38 as ?fg_property_as_item)
BIND(fgp:P38 as ?fg_property_as_p)
BIND(fgpsv:P38 as ?fg_property_as_psv)

# set property of corresponding wikidata property; is constant
BIND(fgt:P343 as ?fg_corr_wd)

SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }

?fg_property_as_item wikibase:propertyType ?fg_property_type .

# transform wikidata qid in factgrid to wikidata entity iri
?link schema:about ?fg_item .
?link schema:isPartOf <https://www.wikidata.org/> .
?link schema:name ?qid.
BIND(IRI(CONCAT(STR(wd:), ?qid)) AS ?wd_item)

# get corresponding wikidata property
?fg_property_as_item ?fg_corr_wd ?fg_corr_wd_value .

# get factgrid statement for time
?fg_item ?fg_property ?fg_value_raw .
?fg_item ?fg_property_as_p [ a wikibase:BestRank ; ?fg_property_as_psv [ wikibase:timePrecision ?fg_value_timeprecision ] ].
# add time precision to fg_value
BIND(CONCAT(STR( ?fg_value_raw ), "/", STR(?fg_value_timeprecision)) AS ?fg_value ) .

# create properties to fetch time info from wikidata
BIND(IRI(CONCAT(STR(wdt:), ?fg_corr_wd_value)) AS ?wd_property)
BIND(IRI(CONCAT(STR(wdp:), ?fg_corr_wd_value)) AS ?wd_property_p)
BIND(IRI(CONCAT(STR(wdpsv:), ?fg_corr_wd_value)) AS ?wd_property_psv)

# now change to wikidata as data source and get value from wikidata
SERVICE <https://query.wikidata.org/sparql> {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
  OPTIONAL {
    ?wd_item ?wd_property ?wd_value_raw.
    ?wd_item ?wd_property_p [ a wikibase:BestRank ; ?wd_property_psv [ wikibase:timePrecision ?wd_value_timeprecision ] ].
   BIND(CONCAT(STR( ?wd_value_raw ), "/", STR(?wd_value_timeprecision)) AS ?wd_value ) .
  }
  }

  # comparison
  BIND (IF(?fg_value = ?wd_value, "true", "false") AS ?is_same) .
}
